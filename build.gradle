import org.apache.tools.ant.filters.*

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.4'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'com.bmuschko.tomcat'

sourceCompatibility = 1.8
version = '1.0'


if (hasProperty("env")) {
    Properties props = new Properties()
    props.load(new FileInputStream(".bsconfig/" + env + ".properties"))
    props.each { prop ->
        project.ext.set(prop.key, prop.value)
    }
}

repositories {
    mavenCentral()
}

processResources {
    filter ReplaceTokens, tokens: [
            "BS_SERVER_URL": bsBackendUrl,
            "LOGS_DIR": logsDir,
            "LOG_LEVEL": logLevel
    ]
}

processTestResources {
    filter ReplaceTokens, tokens: [
            "BS_SERVER_URL_4TESTS": bsBackendUrl_4tests
    ]
}

task printEnv << {
    println bsBackendUrl;
}

dependencies {
    compile group: 'org.apache.httpcomponents', version: '4.5.2', name: 'httpmime'
    compile group: 'org.apache.httpcomponents', version: '4.5.2', name: 'httpclient'
    compile group: 'commons-fileupload', version: '1.3.1', name: 'commons-fileupload'
    compile group: 'javax.ws.rs', version: '2.0.1', name: 'javax.ws.rs-api'
    compile group: 'org.glassfish.jersey.containers', version: '2.23.2', name: 'jersey-container-servlet'
    compile group: 'org.glassfish.jersey.core', version: '2.23.2', name: 'jersey-client'
    compile group: 'org.glassfish.jersey.connectors', version: '2.23.2', name: 'jersey-apache-connector'


    compile group: 'ch.qos.logback', version: '1.1.7', name: 'logback-classic'
    compile group: 'ch.qos.logback', version: '1.1.7', name: 'logback-core'
    compile group: 'org.slf4j', version: '1.7.21', name: 'slf4j-api'
    compile group: 'org.slf4j', version: '1.7.21', name: 'jul-to-slf4j'

    compile group: 'org.json', version: '20140107', name: 'json'
    compile group: 'org.mapdb', version: '2.0-beta13', name: 'mapdb'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '2.0.2-beta'

    providedCompile "javax.servlet:javax.servlet-api:3.0.1"

    def tomcatVersion = '7.0.59'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
}

test {
    systemProperties 'property': 'value'
}

war {
    filter(ReplaceTokens, tokens: ["BS_SERVER_URL": bsBackendUrl])
    webAppDirName = 'WebContent'
    baseName = 'proxy'
    version = ''
    from 'src/main/webapp/'
}

eclipse {
    wtp {
        component {
            contextPath = '/biostudies/submissions'
            resource sourcePath: 'src/main/webapp', deployPath: '/'
            resource sourcePath: 'WebContent', deployPath: '/'
            resource sourcePath: 'build/resources/main', deployPath: '/WEB-INF/classes'
        }

        facet {
            facet name: 'jst.web', version: '3.0'
            facet name: 'java', version: '1.8'
        }

    }

}

tomcat {
    httpPort = 9180
    //httpsPort = 9181
    //enableSSL = true
    contextPath = 'proxy'
}


defaultTasks 'clean', 'build'


